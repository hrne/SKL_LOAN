----- BASIC LABEL Variables ----------
<sub type="SYS">
</sub>

<sub type="SYS" name="label">

!#TXCD
!T(3,L2480)

![資料表頭(HEADER)長度]
#INQHD=n,3,S
E(0,202)ASGN(INQHD$)

![每筆資料明細(OCCURS)長度]
#INQLEN=n,3,S
E(0,0)ASGN(INQLEN$)

![多筆查詢之一個畫面有N筆資料]
#INQREC=n,2,S
E(0,20)ASGN(INQREC$)

![畫面顯示的明細間的高度]
#LOOPH=n,3,S
E(0,1)ASGN(LOOPHEIGHT$)

![每張印錄單列印的筆數]
#INQPRT=n,2,S
E(0,40)ASGN(INQPRT$)

</sub>

<sub type="DC" name="BFHEAD">
<include src="COM.icf::BFHEAD.dec"/>
!起始交易記號  0:起始交易,1:非起始交易
#ORGFG=_,_,S
E(0,0)

</sub>

!---------- 交易畫面及個別交易變數宣告 ----------
<sub type="DC" name="p1" layout="cols=3;screen.width=[150,300,150,300];printer.width=[20,20,20,20];order=1;">
[
["[L2480]                                 擔保品重評資料登錄"],
[#<->#,"#FdFuncd+功能",#FunCdX],
["#FdClCode+擔保品別",#ClCode1,"-",#ClCode2,#ClCodeX],["#FdClNo+擔保品號碼",#ClNo],
[#<->#,"#FdEvaNo+鑑估序號",#EvaNo],
["#FdEvaDate+鑑價日期",#EvaDate],["#FdEvaAmt+評估總價",#EvaAmt],
["#FdEvaNetWorth+評估淨值",#EvaNetWorth],["#FdRentEvaValue+出租評估淨值",#RentEvaValue],
["#FdEvaCompanyId+估價公司代碼",#EvaCompanyId,#BTN1],["#FdEvaCompanyName+估價公司名稱",#EvaCompanyName],
[#<->#,"#FdEvaEmpno+估價人員",#EvaEmpno,#BTN2,#EvaEmpnoX],
[#<->#,"#FdEvaReason+重評原因",#EvaReason,#Reason,#EvaReasonX],
]
!---------- AP Button AREA ----------
![Buttom資料查詢]
#BTN1=X,15,I,ui:button;value:保險/鑑定公司查詢;enable:1;tabbable:0;
#BTN2=X,15,I,ui:button;value:員工資料查詢;enable:1;tabbable:0;
<include src="ROM.icf::L2R22.dec"/>
<include src="ROM.icf::L2R29.dec"/>
<include src="ROM.icf::L2R08.dec"/>
#FdFuncd=X,2,L
T(3,*)

#FdClCode=X,2,L
T(3,*)

#FdClNo=X,2,L
T(3,*)

#FdEvaNo=X,2,L

#FdEvaDate=X,2,L

#FdEvaAmt=X,2,L

#FdEvaNetWorth=X,2,L

#FdRentEvaValue=X,2,L

#FdEvaCompanyId=X,2,L

#FdEvaCompanyName=X,2,L

#FdEvaReason=X,2,L

#FdEvaEmpno=X,2,L

#CleanAll=@,1,S
T(3,@FdEvaDate,)
T(3,@FdEvaAmt,)
T(3,@FdEvaNetWorth,)
T(3,@FdRentEvaValue,)
T(3,@FdEvaCompanyId,)
T(3,@FdEvaCompanyName,)
T(3,@FdEvaReason,)
T(3,@FdEvaEmpno,)
T(F,@EvaReasonX,0)


#SetInput=@,1,S
T(3,@FdEvaDate,*)
T(3,@FdEvaAmt,*)
T(3,@FdEvaCompanyId,*)
T(3,@FdEvaReason,*)
T(3,@FdEvaEmpno,*)

#LabelControl=@,1,S
T(F,@EvaDate,#InputFlag)
T(F,@EvaAmt,#InputFlag)
T(F,@EvaNetWorth,#InputFlag)
T(F,@RentEvaValue,#InputFlag)
T(F,@EvaCompanyId,#InputFlag)
T(F,@EvaCompanyName,#InputFlag)
T(F,@EvaEmpno,#InputFlag)
T(F,@EvaReason,#InputFlag)

#InputFlag=A,1,S
E(0,1)

!---------- AP TEXT AREA Variables ----------
<include src="HELPRIM.icf::HELPRIM.rtn"/>

#SetSysPar=X,1,S
E(0,1)ASGN(AGAIN$)

#FunCdHelp=x,50,S
T(3,1:新增;2:修改;3:複製;4:刪除;5:查詢)

#ClCode1Help=x,50,S
T(3,1:房地;2:土地)

!ClCode21
#CDDEF0001=x,20,S
T(3,CdCode.ClCode21)

#ClCode21Help=x,1024,S

!ClCode22
#CDDEF0002=x,20,S
T(3,CdCode.ClCode22)

#ClCode22Help=x,1024,S

!ClCode23
!#CDDEF0003=x,30,S
!T(3,CdCode.ClCode23)

!#ClCode23Help=x,1024,S

!ClCode24
!#CDDEF0004=x,30,S
!T(3,CdCode.ClCode24)

!#ClCode24Help=x,1024,S

!EvaReason
#CDDEF0005=x,30,S
T(3,CdCode.EvaReason)

#EvaReasonHelp=x,1024,S

#HelpRim=X,1,S
RESET_RIM(#HelpRim,XXR99)
!S(XXR99,01,#CDDEF0001,#CDDEF0002,#CDDEF0003,#CDDEF0004,#CDDEF0005)
S(XXR99,01,#CDDEF0001,#CDDEF0002,#CDDEF0005)
R(1,XXR99)
CALL(#HelpProc)
T(2,@ClCode21Help,#HelpDesc1)
T(2,@ClCode22Help,#HelpDesc2)
T(2,@EvaReasonHelp,#HelpDesc3) 

#CHAIN=A,1,S
T(4,CHAIN$)
IF(#CHAIN!=1,V(P,此為連動交易，請從交易:[L2039擔保品重評明細資料查詢]進入),$)
T(2,@RimTxCode,#TXCD)

#NTXBUF=X,200,S
C(3,#CHAIN,1,T(4,NTXBUF$),S)

#SetData=A,1,S
C(3,#CHAIN,1,T(1,@ChainFunCd,#NTXBUF,1,1),$)
C(3,#CHAIN,1,T(1,@FunCd,#NTXBUF,1,1),$)
C(3,#CHAIN,1,T(1,@ClCode1,#NTXBUF,2,1),$)
C(3,#CHAIN,1,T(1,@ClCode2,#NTXBUF,3,2),$)
C(3,#CHAIN,1,T(1,@ClNo,#NTXBUF,5,7),$)
C(3,#CHAIN,1,T(1,@EvaNo,#NTXBUF,12,2),$)

#ChainFunCd=A,1,S
C(3,#ChainFunCd,1,E(0,@CHAIN,0))

![功能]
#FunCd=A,1,S
CALL(#CleanAll)
CALL(#SetInput)
C(3,#CHAIN,1,s,$)
HELP(#FunCdHelp)
@V(H,#FunCdHelp)
E(0,@FUNCIND,#FunCd)

#SetSysPar1=X,1,S
IF(#FunCd ==1 || #FunCd ==3,E(0,0)ASGN(AGAIN$),$)

!CHAIN進查詢時隱藏按鈕
#chainF5HIDE=X,1,S
C(3,#CHAIN,1,C(2,#FunCd,S,S,S,S,$,$),S)
HIDE(#BTN1,#BTN2)

#AfterFunCd=A,1,S
IF(#FunCd==1,T(F,@ClCode1,1)T(F,@ClCode2,1)T(F,@ClNo,1),T(F,@ClCode1,0)T(F,@ClCode2,0)T(F,@ClNo,0))
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,E(0,@InputFlag,1),E(0,@InputFlag,0))
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,CALL(#SetInput),CALL(#CleanAll))
CALL(#LabelControl)

#FunCdX=X,4,L
C(4,#FunCd,S,$)
T(H,#FunCd,#FunCdHelp)
C(2,#FunCd,$,K(MODIFYBUTTON,新增),K(MODIFYBUTTON,修改),K(MODIFYBUTTON,新增),K(MODIFYBUTTON,刪除),K(MODIFYBUTTON,查詢)K(NOBUTTON,CLOSEY))


![代號1]
#ClCode1=A,1,I
C(3,#ClCode1,0,$,C(3,#CHAIN,1,s,$))
HELP(#ClCode1Help) 
@V(H,#ClCode1Help)

![代號2]
#ClCode2=A,2,I
C(3,#ClCode1,0,$,C(3,#CHAIN,1,s,$))
C(2,#ClCode1,T(3,),HELP(#ClCode21Help),
                   HELP(#ClCode22Help))
!                   HELP(#ClCode23Help),
!                   HELP(#ClCode24Help))
@C(2,#ClCode1,T(3,),V(H,#ClCode21Help),
                    V(H,#ClCode22Help))
!                    V(H,#ClCode23Help),
!                    V(H,#ClCode24Help))

#ClCode=A,3,S
T(2,#ClCode1+#ClCode2)

#ClCodeX=X,13,L
C(2,#ClCode1,T(3,),T(H,#ClCode2,#ClCode21Help),
                   T(H,#ClCode2,#ClCode22Help))
!                   T(H,#ClCode2,#ClCode23Help),
!                   T(H,#ClCode2,#ClCode24Help))

![擔保品號碼]
#ClNo=A,7,I
C(3,#ClCode1,0,$,C(3,#CHAIN,1,s,$))
@V(2,0)

!RIM取擔保主檔
#RimL2r22=X,1,S
IF(#FunCd == 1,$,S)
E(0,@RimClCode1,#ClCode1)
E(0,@RimClCode2,#ClCode2)
E(0,@RimClNo,#ClNo)
E(0,@RimFunCd,5)
RESET_RIM(#RimL2r22,L2R22)
S(L2R22,1,#RimClCode1,#RimClCode2,#RimClNo,#RimFunCd)
R(1,L2R22)

!#checkL2r22=X,1,S
!IF(#FunCd == 1,S,$)
!T(2,@ClTypeCode,#L2r22ClTypeCode)
!T(2,@CityCode,#L2r22CityCode)
!E(0,@ClStatus,#L2r22ClStatus)
!E(0,@EvaDate,#L2r22EvaDate)
!E(0,@EvaAmt,#L2r22EvaAmt)

![鑑估序號]
#EvaNo=A,2,L
C(3,#ClCode1,0,$,C(3,#CHAIN,1,s,$))
C(3,#FunCd,1,s,$)
@V(2,0)

!調RIM參數
#RimFunCd=A,1,S
#RimClCode1=A,1,S
#RimClCode2=A,2,S
#RimClNo=A,7,S
#RimEvaNo=A,2,S


!RIM查擔保品重評資料檔
#RimL2r29EvaNo=X,1,S
C(3,#EvaNo,0,S,$)
E(0,@RimFunCd,#FunCd)
E(0,@RimClCode1,#ClCode1)
E(0,@RimClCode2,#ClCode2)
E(0,@RimClNo,#ClNo)
E(0,@RimEvaNo,#EvaNo)
RESET_RIM(#RimL2r29EvaNo,L2R29)
S(L2R29,1,#RimFunCd,#RimClCode1,#RimClCode2,#RimClNo,#RimEvaNo)
R(1,L2R29)

!RIM查到的資料塞進欄位,新增時不塞
#checkL2r29EvaNo=X,1,S
C(3,#ChainFunCd,1,s,$)
C(3,#EvaNo,0,S,$)
E(0,@EvaDate,#L2r29EvaDate)
E(0,@EvaAmt,#L2r29EvaAmt)
E(0,@EvaNetWorth,#L2r29EvaNetWorth)
E(0,@RentEvaValue,#L2r29RentEvaValue)
T(2,@EvaCompanyId,#L2r29EvaCompanyId)
T(2,@EvaCompanyName,#L2r29EvaCompanyName)
T(2,@EvaEmpno,#L2r29EvaEmpno)
E(0,@EvaReason,#L2r29EvaReason)
T(2,@EvaReasonX,#L2r29OtherReason)
C(3,#ChainFunCd,3,E(0,@EvaNo,00),$)



#SendL2r08B=X,1,S
C(3,#ChainFunCd,1,s,$)
C(4,#EvaEmpno,S,$)
T(2,@RimEmployeeNo,#EvaEmpno)
S(L2R08,1,#RimTxCode,#RimEmployeeNo)
R(1,L2R08)

#checkSendL2r08B=X,1,S
C(3,#ChainFunCd,1,s,$)
C(4,#EvaEmpno,S,$)
T(2,@EvaEmpnoX,#L2r08FullName)


![鑑價日期]
#EvaDate=D,7,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
@V(7)A(DATE,0,#EvaDate)

![評估總價]
#EvaAmt=m,14,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
@V(2,0)

!評估淨值
#EvaNetWorth=m,14,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)

!出租評估淨值
#RentEvaValue=m,14,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)

![瀏覽1]
#BUF1A=X,4,S
T(2,2+  +0)

#BIND1A=X,1,S
BIND(#BTN1,click, {cmd=CHAIN; ntxcd=L6073; ntxbuf=#BUF1A;ntxbuf5=type<-1:EvaCompanyId<-OOInsurerCode:EvaCompanyName<-OOInsurerItem})

![估價公司統編]
#EvaCompanyId=X,2,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
@V(7)

#RimFuncCode=A,1,S
#RimL6R10InsurerType=X,1,S
#RimL6R10InsurerCode=X,2,S

#SendL6R10=X,1,S
E(0,@RimFuncCode,5)
T(2,@RimL6R10InsurerType,2)
T(2,@RimL6R10InsurerCode,#EvaCompanyId)
RESET_RIM(#SendL6R10,L6R10)
S(L6R10,1,#RimFuncCode,#RimTxCode,#RimL6R10InsurerType,#RimL6R10InsurerCode)
R(1,L6R10)

![估價公司名稱]
#EvaCompanyName=x,100,L
T(2,#L6R10InsurerItem)

![瀏覽2]
#BUF2A=X,7,S
T(2,      Y)
#BIND2A=X,1,S
BIND(#BTN2,click, {cmd=CHAIN; ntxcd=L6088; ntxbuf=#BUF2A;ntxbuf5=type<-1:EvaEmpno<-OOEmployeeNo})

![估價人員]
#EvaEmpno=X,6,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
@V(7)

#RimEmployeeNo=X,6,S
#RimTxCode=X,5,S

#SendL2r08A=X,1,S
C(4,#EvaEmpno,S,$)
T(2,@RimEmployeeNo,#EvaEmpno)
S(L2R08,1,#RimTxCode,#RimEmployeeNo)
R(1,L2R08)

#EvaEmpnoX=X,20,L
C(4,#EvaEmpno,S,$)
T(2,#L2r08FullName)

![重評原因]
#EvaReason=A,2,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
HELP(#EvaReasonHelp)
@
C(3,#FunCd,2,IF (#EvaReason==#L2r29EvaReason,s,$),$)
V(H,#EvaReasonHelp)

#REASON_LB_CONTROL=X,1,S
IF(#EvaReason==99 && #FunCd <= 2,T(F,@EvaReasonX,1),T(F,@EvaReasonX,0))

#Reason=x,10,L
T(H,#EvaReason,#EvaReasonHelp)

#EvaReasonX=x,100,I
IF(#FunCd==1 || #FunCd==2|| #FunCd==3,$,s)
IF(#EvaReason==99,$,S)
@V(7)

#RPTFG=A,1,S
<include src="ROM.icf::L6R10.dec"/>

</sub>

!---------- 輸出共用變數宣告區 ----------
<sub type="RTN" name="AFXMT">
<include src="PRT.icf::PRTHEAD.rtn"/>
</sub>


!---------- TXFORM = L2480 ----------
<sub type="FORM" name="L2480">

!#SCRPRT=_,_,S
!C(5,#TXFORM,L2480,$,S)

#SCRTIL=_,_,S
T(3,[L2480] 擔保品重評資料登錄)

!------ TOM(HEAD)   Variables (FOR OUTPUT SCREEN) ------
#OWarningMsg=x,200,S
C(4,#OWarningMsg,$,ALERT(#OWarningMsg))


#OEvaNo=A,2,S
E(0,@EvaNo,#OEvaNo)


!------ TOM(OCCURS) Variables (FOR OUTPUT SCREEN) ------

</sub>


!------ 上行電文 ------
<sub type="TIM">
#FunCd
#ClCode1
#ClCode2
#ClNo
#EvaNo
#EvaDate
#EvaAmt
#EvaNetWorth
#RentEvaValue
#EvaCompanyId
#EvaCompanyName
#EvaEmpno
#EvaReason
#EvaReasonX
</sub>


!------ 下行電文TOM ------
<sub type="TOM">
TXFORM=L2480
#OWarningMsg
#OEvaNo
^
<include src="COM.icf::TRCTL.tom"/>
<include src="ROM.icf::L2R22.tom"/>
<include src="ROM.icf::L2R29.tom"/>
<include src="ROM.icf::L2R08.tom"/>
<include src="ROM.icf::L6R10.tom"/>
<include src="HELPRIM.icf::HELPRIM.tom"/>
</sub>

!---------- 單據輸出組合 ----------
<sub type="SELECT">
<include src="PRT.icf::UPD01.sel"/>
</sub>

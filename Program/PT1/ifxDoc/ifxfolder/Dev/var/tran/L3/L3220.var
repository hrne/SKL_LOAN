!---------- BASIC LABEL Variables ----------
<sub type="SYS">
</sub>

<sub type="SYS" name="label">

!#TXCD
!T(3,XW111)

![一段式/二段式/三段式]
!#RELCD=_,_,S
!E(0,1)

!#ACTFG=_,_,S
!E(0,1)

![借貸別 (1:借 2:貸)]
#CRDB=_,_,S
E(0,1)

</sub>

<sub type="DC" name="BFHEAD">
<include src="COM.icf::BFHEAD.dec"/>
!起始交易記號  0:起始交易,1:非起始交易
#ORGFG=_,_,S
E(0,0)
K(MODIFYBUTTON,登錄)
INVOKEJS(SHOW,p7,0)
</sub>
<sub type="DC" name="BFRP">
<include src="RPDTL.icf::BFRP.dec"/>
</sub>
!=====================================================================
!#FKEY>0時，重新執行
!=====================================================================
<sub type="PREDC" name="PreDC">
!#PDC=X,1,S
!C(3,#FKEY,0,NOP,T(R,#MRKEY,#REFNO))
!C(3,#FKEY,3,NO_RUN(#CIFKEY,#PREFG,#KINDCD,#REFNO),$)
!C(3,#FKEY,7,NO_RUN(#CIFKEY,#PREFG,#KINDCD,#REFNO),s)

<include src="COM.icf::TITLE-RUN.pre"/>
</sub>

!---------- 交易畫面及個別交易變數宣告 ----------
<sub type="DC" name="p1" layout="cols=1;screen.width=[150,1000];printer.width=[20,80];">
[
["[L3220]                                 暫收款退還"],
["#FdCustNo+借戶戶號",#CustNo,"@@@",#BTN1,#CustNoX],
["#FdCustId+統一編號",#CustId,"@",#BTN2,#CustIdX],
["#FdApplNo+核准編號",#ApplNo,"@@@",#BTN3,#ApplNoX],
["#FdTempItemCode+作業項目",#TempItemCode,#TempItemCodeX],
["#FdTempReasonCode+暫收帳戶",#TempReasonCode,#TempReasonCodeX],
["#FdCurrencyCode+幣別",#CurrencyCode],
["#FdFacmNo+額度編號",#FacmNo,#TmpFacmNoXX],
["#FdTempAmt+暫收款金額",#TwTempAmt,#UsTempAmt,#ExcessiveXX],
["#FdChequeAcct+支票帳號",#ChequeAcct,#BTN4],
["#FdChequeNo+支票號碼",#ChequeNo],
["#FdChequeAmt+支票金額",#ChequeAmtX],
!["#FdTempDrawCode+代收抽退票",#TempDrawCode,#TempDrawCodeX],
["#FdDescription+摘要內容",#Description],
]

!---------- AP Button AREA ----------

![Buttom資料查詢]
#BTN1=X,15,I,ui:button;value:額度資料查詢;enable:1;tabbable:0;title: L2015 額度明細資料查詢
#BTN2=X,15,I,ui:button;value:顧客資料查詢;enable:1;tabbable:0;title: L1001 顧客明細資料查詢;
#BTN3=X,15,I,ui:button;value:核准號碼查詢;enable:0;tabbable:0;title: L2016 核准號碼明細資料查詢;
#BTN4=X,15,I,ui:button;value:支票資料查詢;enable:1;tabbable:0;title: L3007 支票查詢;
#BUF1=X,42,S

!---------- AP TEXT AREA Variables ----------
! Help
<include src="HELPRIM.icf::HELPRIM.rtn"/>
!CurrencyCode幣別名稱
#CDDEF01=x,30,S
T(3,CdCode.Currency)
#CurrencyCodeHelp=x,1024,S

!幣別小數位
#CDDEF02=x,26,S
T(3,CdCode.CurrPrecision)
#CurrPrecisionHelp=x,1024,S

!作業項目
#CDDEF03=x,26,S
T(3,CdCode.TempItemCode)
#TempItemCodeHelp=x,1024,S

!暫收帳戶
#CDDEF04=x,26,S
T(3,CdCode.Temp2ReasonCode)
#Temp2ReasonCodeHelp=x,1024,S

![匯款人身份別]
#CDDEF05=x,30,S
T(3,CdCode.CustType)
#CustTypeHelp=x,1024,S

!Sex
#CDDEF06=x,20,S
T(3,CdCode.Sex)
#SexHelp=x,1024,S

#HelpRim=X,1,S
RESET_RIM(#HelpRim,XXR99)
S(XXR99,01,#CDDEF01,#CDDEF02,#CDDEF03,#CDDEF04,#CDDEF05,#CDDEF06)
R(1,XXR99)
CALL(#HelpProc) 
T(2,@CurrencyCodeHelp,#HelpDesc1)
T(2,@CurrPrecisionHelp,#HelpDesc2)
T(2,@TempItemCodeHelp,#HelpDesc3)
T(2,@Temp2ReasonCodeHelp,#HelpDesc4)
T(2,@CustTypeHelp,#HelpDesc5)
T(2,@SexHelp,#HelpDesc6)

#CHAIN=A,1,S
T(4,CHAIN$)
T(2,@RimTxCode,#TXCD)
E(0,@RimFKey,#FKEY)
HIDE(#UsTempAmt)

#NTXBUF=X,31,S
C(3,#CHAIN,1,T(4,NTXBUF$),S)

#BUF1A=X,30,S
T(3,00000019999999000)

#BIND1A=X,1,S
BIND(#BTN1,click, {cmd=CHAIN; ntxcd=L2015; ntxbuf=#BUF1A;ntxbuf5=type<-1:CustNo<-OOCustNo:CustNoX<-OOCustName})

#InitFd=X,1,S
T(3,@FdTempReasonCode,*)
T(3,@FdTempItemCode,*)

![借戶戶號]
!借戶戶號, 統一編號或核准編號擇一輸入
#FdCustNo=X,2,L

#CustNo=X,7,I
SHOW(#BTN1)HIDE(#BTN2,#BTN3)

#CustNo2=A,7,S
C(4,#CustNo,S,$)
E(0,#CustNo)
T(2,@CustNo,#CustNo2)
T(3,#CustNoX,)

#InitFdB=X,1,S
T(3,@FdApplNo, )

#SendL2r07A=X,1,S
C(4,#CustNo,S,$)
T(3,@RimCustId, )
E(0,@RimCustNo,#CustNo)
RESET_RIM(#SendL2r07A,L2R07)
S(L2R07,1,#RimTxCode,#RimCustId,#RimCustNo)
R(1,L2R07)
T(2,@CustNoX,#L2r07CustName)

#CustNoX=x,100,L
C(4,#CustNo,S,$)

#WKBTN1=X,1,S
C(4,#CustNo,S,HIDE(#BTN1),SHOW(#BTN1))

#BUF2A=X,100,S

#BIND2A=X,1,S
BIND(#BTN2,click, {cmd=CHAIN; ntxcd=L1001; ntxbuf=#BUF2A;ntxbuf5=type<-1:CustId<-OOCustId:CustIdX<-OOCustName})

![統一編號]
#FdCustId=X,2,L

#CustId=X,10,I
C(3,#CHAIN,1,s,$)
C(4,#CustNo,$,S)
SHOW(#BTN2)HIDE(#BTN1,#BTN3)
@C(4,#CustId,S,$)
V(7)
T(3,#CustIdX,)

#BUF2B=X,100,S
T(2,0000000+#CustId)

#BIND2B=X,1,S
BIND(#BTN2,click, {cmd=CHAIN; ntxcd=L1001; ntxbuf=#BUF2B;ntxbuf5=type<-1:CustId<-OOCustId:CustIdX<-OOCustName})

#CheckCustId=X,1,S
C(4,#CustId,S,$)
A(ID_UNINO,1,#CustId)

#WKBTN2=X,1,S
C(4,#CustId,HIDE(#BTN2),SHOW(#BTN2))

#SendL2r07B=X,1,S
C(4,#CustId,S,$)
T(2,@RimCustId,#CustId)
E(0,@RimCustNo,0)
RESET_RIM(#SendL2r07B,L2R07)
S(L2R07,1,#RimTxCode,#RimCustId,#RimCustNo)
R(1,L2R07)
T(2,@CustIdX,#L2r07CustName)

#CustIdX=x,100,L
C(4,#CustId,S,$)

#WkStartDate=D,7,S
E(0,#SYSDATE)

#BUF3A=X,45,S
T(3,000000199999990)

#BIND3A=X,1,S
BIND(#BTN3,click, {cmd=CHAIN; ntxcd=L2016; ntxbuf=#BUF3A;ntxbuf5=type<-1:ApplNo<-OOApplNo:ApplNoX<-OOCustName})

![核准編號]
#FdApplNo=X,2,L
C(3,#CHAIN,1,S,$)
C(4,#CustNo,$,S)
C(4,#CustId,$,S)
T(3,*)

#ApplNo=X,7,I
C(3,#CHAIN,1,s,$)
C(4,#CustNo,$,S)
C(4,#CustId,$,S)
SHOW(#BTN3)HIDE(#BTN1,#BTN2)
@C(4,#ApplNo,V(P,需擇一輸入),$)

#ApplNo2=A,7,S
C(4,#ApplNo,S,$)
E(0,#ApplNo)
T(2,@ApplNo,#ApplNo2)
T(3,#ApplNoX,)

#BUF3B=X,45,S
T(2,0000000000000000000000000000          +#ApplNo)

#BIND3B=X,1,S
BIND(#BTN3,click, {cmd=CHAIN; ntxcd=L2016; ntxbuf=#BUF3B;ntxbuf5=type<-1:ApplNo<-OOApplNo})

!用核准號碼(ApplNo)調L2R05
#SendL2r05A=X,1,S
C(4,#ApplNo,S,$)
E(0,@RimFuncCode,5)
E(0,@RimCustNo,0)
E(0,@RimFacmNo,0)
E(0,@RimApplNo,#ApplNo)
E(0,@RimCaseNo,0)
RESET_RIM(#SendL2r05A,L2R05)
S(L2R05,1,#RimFuncCode,#RimTxCode,#RimFKey,#RimCustNo,#RimFacmNo,#RimApplNo,#RimCaseNo)
R(1,L2R05)

#SendL2r07C=X,1,S
C(3,#CustNo,0,$,S)C(4,#CustId,$,S)
T(2,@RimCustId,#L2r05CustId)
E(0,@RimCustNo,#CustNo)
RESET_RIM(#SendL2r07C,L2R07)
S(L2R07,1,#RimTxCode,#RimCustId,#RimCustNo)
R(1,L2R07)
T(2,@ApplNoX,#L2r07CustName)

#ApplNoX=x,100,L
C(4,#ApplNo,S,$)

#TimCustNo=A,7,S
E(0,0)
C(4,#CustNo,$,E(0,#CustNo))
C(4,#CustId,$,E(0,#L2r07CustNo))
C(4,#ApplNo,$,E(0,#L2r05CustNo))
T(2,@LockCustNo,#TimCustNo)
T(2,@RmkCustNo,#TimCustNo)
<include src="COM.icf::LockControl.rtn"/>
<include src="COM.icf::CustRmk.rtn"/>


#TimCustNoX=x,100,S
C(4,#CustNo,$,T(2,#CustNoX))
C(4,#CustId,$,T(2,#CustIdX))
C(4,#ApplNo,$,T(2,#ApplNoX))

!支票查詢按鈕
#BUF4=X,45,S
T(2,0000000+#TimCustNo+          +0000000+0)
#BIND4=X,1,S
BIND(#BTN4,click, {cmd=CHAIN; ntxcd=L3007; ntxbuf=#BUF4;ntxbuf5=type<-1:ChequeAcct<-OOChequeAcct:ChequeNo<-OOChequeNo})

![作業項目]
!暫收款金額只可做4~29項目
!支票金額只可做1,2,3項目
#FdTempItemCode=X,2,L
T(3,*)
#RimTempItemCode=A,2,S
#TempItemCode=A,2,I
HELP(#TempItemCodeHelp)
@V(H,#TempItemCodeHelp)
! C(3,#TimTempAmt,0,$,V(1,4,5,11))
! C(3,#ChequeAmt,0,$,V(1,1,2))
IF(#TempItemCode==01 ||#TempItemCode==02,T(F,@FacmNo,0)E(0,@FacmNo,0),T(F,@FacmNo,1))
C(4,#Description,C(3,#TempItemCode,2,T(3,@Description,退票原因- -存款不足或拒往),$),T(3,@Description,))

#TempItemCodeX=X,20,L
T(3,)T(H,#TempItemCode,#TempItemCodeHelp)

#showbtn4=X,1,S
SWITCH(#TempItemCode,01,SHOW(#BTN4)INVOKEJS(SHOW,p1,8,8,0)INVOKEJS(SHOW,p1,9,11,1)
                    ,02,SHOW(#BTN4)INVOKEJS(SHOW,p1,8,8,0)INVOKEJS(SHOW,p1,9,11,1)
                    ,03,SHOW(#BTN4)INVOKEJS(SHOW,p1,8,8,0)INVOKEJS(SHOW,p1,9,11,1)
                    ,04,HIDE(#BTN4)INVOKEJS(SHOW,p1,8,8,1)INVOKEJS(SHOW,p1,9,11,0)
                    ,05,HIDE(#BTN4)INVOKEJS(SHOW,p1,8,8,1)INVOKEJS(SHOW,p1,9,11,0)
                    ,11,HIDE(#BTN4)INVOKEJS(SHOW,p1,8,8,1)INVOKEJS(SHOW,p1,9,11,0))

![暫收帳戶]
#FdTempReasonCode=X,2,L
T(3,*)

#TempReasonCode=A,1,I
HELP(#Temp2ReasonCodeHelp)
C(3,#TempReasonCode,0,E(0,1),$)
C(3,#TempItemCode,1,E(0,1),$)
C(3,#TempItemCode,2,E(0,1),$)
C(3,#TempItemCode,1,s,$)
C(3,#TempItemCode,2,s,$)
@V(H,#Temp2ReasonCodeHelp)

#TempReasonCodeX=X,16,L
T(3,)T(H,#TempReasonCode,#Temp2ReasonCodeHelp) 

![業務別] 01:撥款匯款(含暫收退還且非退票) 02:支票繳款  09:放款
#SECNO=_,_,S
T(3,01)
C(3,#TempItemCode,1,T(3,09),$)
C(3,#TempItemCode,2,T(3,09),$)
C(3,#TempItemCode,3,T(3,09),$)

#SendL3r05=X,1,S
E(0,@RimCustNo,#TimCustNo)
E(0,@RimFacmNo,#TimFacmNo)
E(0,@RimTempReasonCode,#TempReasonCode)
E(0,@RimTempItemCode,#TempItemCode)
RESET_RIM(#SendL3r05,L3R05)
S(L3R05,1,#RimTxCode,#RimCustNo,#RimFacmNo,#RimTempReasonCode,#RimTempItemCode)
R(1,L3R05)
C(4,#L3r05TmpFacmNoX,T(2,@TmpFacmNoXX,),T(B,@TmpFacmNoXX,暫收指定額度：+#L3r05TmpFacmNoX))
E(0,@Excessive,#L3r05TempAmt)
E(0,@ExcessiveAll,#L3r05ExcessiveAll)
CALL2(#ADJUSTDOT,#Precision,#Excessive,#ExcessiveX)
CALL2(#ADJUSTDOT,#Precision,#ExcessiveAll,#ExcessiveAllX)
T(B,@ExcessiveXX,#ExcessiveX+（全戶：+#ExcessiveAllX+）)

#TmpFacmNoXX=X,60,L

![幣別]
#FdCurrencyCode=X,2,L

#CurrencyCode=X,3,L
T(3,TWD)

#Precision=N,1,S
E(0,0)T(H,#CurrencyCode,#CurrPrecisionHelp)

#CurrencyCodeX=X,10,S
T(3,)T(H,#CurrencyCode,#CurrencyCodeHelp) 

![SWIFT幣別三碼]
#CURNM=_,_,S
T(2,#CurrencyCode)

#WkHideAmt=X,1,S
C(3,#Precision,0,SHOW(#TwTempAmt)
                 HIDE(#UsTempAmt)
                ,HIDE(#TwTempAmt)
                 SHOW(#UsTempAmt))

![額度編號]
!作業項目=9,10,25,29必須輸入,其他不可輸入
#FdFacmNo=X,2,L

#FacmNo=A,3,I
IF(#TempItemCode==01 ||#TempItemCode==02,S,$)
T(3,@RelationName,)

#TimFacmNo=A,3,S
C(4,#ApplNo,E(0,#FacmNo),E(0,#L2r05FacmNo))

![暫收款金額]
!退還暫收款金額不可>該戶號目前暫收金額
#FdTempAmt=X,2,L

#TwTempAmt=m,14,I
C(3,#L3r05ExcessiveAll,0,S,$)
C(3,#Precision,0,$,s)
C(3,#TempItemCode,1,S,$)
C(3,#TempItemCode,2,S,$)
C(3,#TempItemCode,3,S,$)
@
V(2,0)
V(5,0,#L3r05ExcessiveAll)

#UsTempAmt=m,14.2,I
C(3,#L3r05ExcessiveAll,0,S,$)
C(3,#Precision,0,s,$)
C(3,#TempItemCode,1,S,$)
C(3,#TempItemCode,2,S,$)
C(3,#TempItemCode,3,S,$)
@
V(2,0)
V(5,0,#L3r05ExcessiveAll)

#TimTempAmt=m,14.2,S
C(3,#Precision,0,E(0,#TwTempAmt),E(0,#UsTempAmt))
C(3,#TimTempAmt,0,T(3,@FdChequeAcct,*)T(3,@FdChequeNo,*)
                 ,T(3,@FdChequeAcct, )T(3,@FdChequeNo, ))

#FdExcessive=X,2,L

#Excessive=m,14.2,S
E(0,#L3r05TempAmt)
#ExcessiveAll=m,14.2,S
E(0,#L3r05ExcessiveAll)

#ExcessiveX=X,22,L
CALL2(#ADJUSTDOT,#Precision,#Excessive,#ExcessiveX)
#ExcessiveAllX=X,22,L
CALL2(#ADJUSTDOT,#Precision,#ExcessiveAll,#ExcessiveAllX)
#ExcessiveXX=X,50,L

#WKHIDECHQAMT=X,1,S

![支票帳號]
#FdChequeAcct=X,2,L
C(3,#TimTempAmt,0,T(3,*),T(3, ))

#ChequeAcct=A,9,I
C(3,#TimTempAmt,0,$,S)
C(3,#TempItemCode,4,S,$)
C(3,#TempItemCode,5,S,$)
C(3,#TempItemCode,11,S,$)
@V(2,0)

![支票號碼]
#FdChequeNo=X,2,L
C(3,#TimTempAmt,0,T(3,*),T(3, ))

#ChequeNo=A,7,I
C(3,#TimTempAmt,0,$,S)
C(3,#TempItemCode,4,S,$)
C(3,#TempItemCode,5,S,$)
C(3,#TempItemCode,11,S,$)
@V(2,0)

#SendL3r04=X,1,S
C(3,#TempItemCode,4,S,$)
C(3,#TempItemCode,5,S,$)
C(3,#TempItemCode,11,S,$)
E(0,@RimFuncCode,5)
E(0,@RimChequeAcct,#ChequeAcct)
E(0,@RimChequeNo,#ChequeNo)
RESET_RIM(#SendL3r04,L3R04)
S(L3R04,1,#RimFuncCode,#RimTxCode,#RimChequeAcct,#RimChequeNo)
R(1,L3R04)

![支票金額]
#FdChequeAmt=X,2,L

#ChequeAmt=m,14.2,S
C(3,#TimTempAmt,0,$,S)
E(0,#L3r04ChequeAmt)
C(3,#TempItemCode,4,S,$)
C(3,#TempItemCode,5,S,$)
C(3,#TempItemCode,11,S,$)

#ChequeAmtX=X,22,L
C(3,#TimTempAmt,0,$,S)
CALL2(#ADJUSTDOT,#Precision,#ChequeAmt,#ChequeAmtX)

![交易金額]
#TXAMT=_,_,S
C(3,#TimTempAmt,0,E(0,#ChequeAmt),E(0,#TimTempAmt))
                   
![代收抽退票]
!作業項目=3必須輸入,其他不可輸入
!#FdTempDrawCode=X,2,L
!C(3,#TempItemCode,3,T(3,*)INVOKEJS(SHOW,p1,11,11,1),T(3, )INVOKEJS(SHOW,p1,11,11,0))

!#TempDrawCode=X,1,I
!C(3,#TempItemCode,3,$,S)
!HELP(L3DEF,TempDrawCodeDef,TempDrawCode,TempDrawCodeX)
!@V(E,0,L3DEF,TempDrawCodeDef)

!#TempDrawCodeX=X,4,L
!C(3,#TempItemCode,3,$,S)
!T(A,#TempDrawCode,L3DEF,TempDrawCodeDef,TempDrawCode,TempDrawCodeX)

!戶名
#RpCustName=x,100,L
C(4,#CustNo,$,T(2,@RpCustName,#CustNoX))
C(4,#CustId,$,T(2,@RpCustName,#CustIdX))
C(4,#ApplNo,$,T(2,@RpCustName,#ApplNoX))
T(2,@RP_CUSTNM,#RpCustName)

#MRKEY=_,_,S
C(3,#TimFacmNo,0,T(2,#TimCustNo),T(2,#TimCustNo+-+#TimFacmNo))

!用核准號碼(ApplNo)調L2R05
#SendL2r05B=X,1,S
IF(#TempItemCode==01 ||#TempItemCode==02,S,$)
C(3,#FacmNo,0,S,$)
E(0,@RimFuncCode,5)
E(0,@RimCustNo,#TimCustNo)
E(0,@RimFacmNo,#FacmNo)
E(0,@RimApplNo,0)
E(0,@RimCaseNo,0)
RESET_RIM(#SendL2r05B,L2R05)
S(L2R05,1,#RimFuncCode,#RimTxCode,#RimFKey,#RimCustNo,#RimFacmNo,#RimApplNo,#RimCaseNo)
R(1,L2R05)
T(2,@RelationName,#L2r05RelationName)

![摘要內容]
#FdDescription=X,2,L

#Description=X,30,I

!應收付類別,5:暫收款退還 7:暫收轉帳 8:抽退票
#TempRPFG=X,1,S
E(0,@RPFG,0)
C(3,#TempItemCode,1,E(0,@RPFG,8),$)
C(3,#TempItemCode,2,E(0,@RPFG,8),$)
C(3,#TempItemCode,3,E(0,@RPFG,8),$)
C(3,#TempItemCode,4,E(0,@RPFG,5),$)
C(3,#TempItemCode,5,E(0,@RPFG,5),$)
C(3,#TempItemCode,6,E(0,@RPFG,7),$)
C(3,#TempItemCode,11,E(0,@RPFG,5),$)

!還款來源,04:退款台新 05:退款他行 11:退款新光 92:暫收轉帳 93:抽退票
#TempRPTYP=X,1,S
E(0,@RPTYPZ,0)
C(3,#TempItemCode,1,E(0,@RPTYPZ,93),$)
C(3,#TempItemCode,2,E(0,@RPTYPZ,93),$)
C(3,#TempItemCode,3,E(0,@RPTYPZ,93),$)
C(3,#TempItemCode,4,E(0,@RPTYPZ,4),$)
C(3,#TempItemCode,5,E(0,@RPTYPZ,5),$)
C(3,#TempItemCode,6,E(0,@RPTYPZ,92),$)
C(3,#TempItemCode,11,E(0,@RPTYPZ,11),$)
!應收付總金額,轉暫收金額
#TempRPTOT=X,1,S
E(3,@RPTOT,#TimTempAmt+#ChequeAmt)

!實際收付金額,轉暫收金額
#TempRPAMT=X,1,S
E(3,@RPAMTZ,#TimTempAmt+#ChequeAmt)

! ----------3:期票、8:抽退票，需輸入----------
! 戶號
#TmpCustNo=X,1,S
E(0,@RP_CUSTNO,#TimCustNo)

! 額度
#TempFacmNo2=X,1,S
E(0,@RP_FACMNO,0)

! 支票帳號
#TempChequeAcct=X,1,S
E(0,#RP_CHQACT,#ChequeAcct)

! 支票號碼 
#TempChequeNo=X,1,S
E(0,@RP_CHQUENO,#ChequeNo)

! 支票金額   
#TempChequeAmt=X,1,S
E(0,@RP_CHQUEAMT,#ChequeAmt)

#WK1=X,1,S
C(3,#RPFG,0,$,T(3,@BATCHNO,LN01))





<include src="RPDTL.icf::RPCOL.dec"/>


#RelationName=x,200,S


#checkAMLName=X,1,S
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RpCustName1!=#TimCustNoX&&#RpCustName1!=#RelationName,INVOKEJS(SHOW,p7,1),INVOKEJS(SHOW,p7,0))




#INITp7=X,1,S
C(4,#INITp7,T(3,@RpRemitIdKind, )
            T(3,@RpRemitIdKindX, )
            T(3,@RpRemitId, )
            T(3,@RpRemitGender, )
            T(3,@RpRemitGenderX, )
            E(0,@RpRemitBirthday,0),$)


!匯款人身份別
#RpRemitIdKind=X,1,I
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RpCustName1!=#TimCustNoX&&#RpCustName1!=#RelationName,$,S)
HELP(#CustTypeHelp)
@
C(4,#RpRemitIdKind,S,$)
V(H,#CustTypeHelp)

#RpRemitIdKindX=X,6,L
T(3,)T(H,#RpRemitIdKind,#CustTypeHelp)
!身份證/居留證號碼
#RpRemitId=X,10,I
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RpCustName1!=#TimCustNoX&&#RpCustName1!=#RelationName,$,S)
@C(4,#RpRemitId,s,$)
IF(#RpRemitIdKind=="1",A(ID,0,#RpRemitId),A(ID_UNINO,1,#RpRemitId))

!性別
#RpRemitGender=X,1,I
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RpCustName1!=#TimCustNoX&&#RpCustName1!=#RelationName,$,S)
HELP(#SexHelp)
@
C(4,#RpRemitGender,T(3,@RpRemitGenderX,),$)
C(4,#RpRemitGender,S,$)
V(H,#SexHelp)

#RpRemitGenderX=X,2,L
C(4,#RpRemitGender,S,$)
T(H,#RpRemitGender,#SexHelp)

! 系統日
#wksysdate=D,7,S
E(0,#SYSCDATE)
!出生日期
#RpRemitBirthday=D,7,I
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RpCustName1!=#TimCustNoX&&#RpCustName1!=#RelationName,$,S)
@A(DATE,0,#RpRemitBirthday)
IF(#RpRemitBirthday>#wksysdate,V(P,出生日期不可大於日曆日),$)

!AML處理
!-----------------------------------------------------------------
!AML參數
#RemitIdKind=X,1,S
#RemitId=X,10,S
#RemitGender=X,1,S
#RemitBirthday=D,7,S

#checkaml=X,1,S
!匯款戶名=借款人戶名
IF(#TempItemCode==4||#TempItemCode==5||#TempItemCode==11,$,S)
IF(#RP_AmlCheckName==#TimCustNoX,T(2,@RemitIdKind,#L2r07RemitIdKind)
                                 T(2,@RpRemitIdKind,#L2r07RemitIdKind)
                                 T(H,@RpRemitIdKindX,#RpRemitIdKind,#CustTypeHelp)
                                 T(2,@RemitId,#L2r07RemitId)
                                 T(2,@RpRemitId,#L2r07RemitId)
                                 T(2,@RemitGender,#L2r07RemitGender)
                                 T(2,@RpRemitGender,#L2r07RemitGender)
                                 T(H,@RpRemitGenderX,#RpRemitGender,#SexHelp)
                                 E(0,@RemitBirthday,#L2r07RemitBirthday)
                                 E(0,@RpRemitBirthday,#L2r07RemitBirthday),$)
!匯款戶名=銀扣戶名
IF(#RP_AmlCheckName==#RelationName,
                                   T(H,@RpRemitIdKindX,#RpRemitIdKind,#CustTypeHelp)
                                   T(2,@RemitId,#L2r05RelationId)
                                   T(2,@RpRemitId,#L2r05RelationId)
                                   T(2,@RemitGender,#L2r05RelationGender)
                                   T(2,@RpRemitGender,#L2r05RelationGender)
                                   T(H,@RpRemitGenderX,#RpRemitGender,#SexHelp)
                                   E(0,@RemitBirthday,#L2r05RelationBirthday)
                                   E(0,@RpRemitBirthday,#L2r05RelationBirthday),$)
!匯款戶名=其他戶名
IF(#RP_AmlCheckName!=#TimCustNoX&&#RP_AmlCheckName!=#RelationName,T(2,@RemitIdKind,#RpRemitIdKind)
                                                                  T(2,@RemitId,#RpRemitId)
                                                                  T(2,@RemitGender,#RpRemitGender)
                                                                  E(0,@RemitBirthday,#RpRemitBirthday),$)


<include src="ROM.icf::L0Rim.dec"/>
<include src="ROM.icf::L2Rim.dec"/>
<include src="ROM.icf::L2R05.dec"/>
<include src="ROM.icf::L2R07.dec"/>
<include src="ROM.icf::L2R09.dec"/>
<include src="ROM.icf::L3Rim.dec"/>
<include src="ROM.icf::L3R04.dec"/>
<include src="ROM.icf::L3R05.dec"/>
</sub>
!--------- 收付欄 -----------------------
<sub type="DC" name="RPDTL" layout="cols=1;screen.width=[1000];printer.width=[20,80];">
[
<include src="RPDTL.icf::RPDTL.scr"/>
]
</sub>

!-------- AML處理 -----------------------
<sub type="DC" name="p7" layout="cols=2;screen.width=[250,300,200,300];printer.width=[40,60];order=1;">
[
["@                                                                                                "],
[#<->#,"@@匯款人資料"],
[#<->#,"@@@@@匯款人身份別",#RpRemitIdKind,#RpRemitIdKindX],
[#<->#,"@@@@@匯款人身份證/居留證號碼",#RpRemitId],
[#<->#,"@@@@@匯款人性別",#RpRemitGender,#RpRemitGenderX],
[#<->#,"@@@@@匯款人出生日期",#RpRemitBirthday],
]
</sub>
!---------- 輸出共用變數宣告區 ----------
<sub type="RTN" name="AFXMT">
<include src="PRT.icf::PRTHEAD.rtn"/>
<include src="PRT.icf::FM101.rtn"/>
</sub>


!---------- TXFORM = L3220 ----------
<sub type="FORM" name="L3220">

#SCRPRT=_,_,S
C(5,#TXFORM,L3220,$,S)

#SCRTIL=_,_,S
T(3,[L3220] 暫收款退還)

! 匯款單FileNo
#PdfSnoF=X,8,S
E(2,@Sno,#PdfSnoF)
#Sno=A,8,S

! 匯款單FileNo
#PdfPrtF=A,1,S
C(3,#Sno,0,E(0,0),E(0,1))

</sub>
!---------- TXFORM = FM101 交易分錄----------
<sub type="FORM" name="FM101">
<include src="PRT.icf::FM101.form"/>
</sub>
!------ 上行電文 TEXT ------
<sub type="TIM">
#TimCustNo#TimFacmNo#CurrencyCode#TimTempAmt
#ChequeAcct#ChequeNo#ChequeAmt
#TempReasonCode#TempItemCode#Description
<include src="RPDTL.icf::RPDTL.tim"/>
#CustNo#CustId#ApplNo
#TwTempAmt#UsTempAmt
#RemitIdKind#RemitId#RemitGender#RemitBirthday
</sub>


!------ 下行電文TOM ------
<sub type="TOM">
TXFORM=L3220
#PdfSnoF
^
<include src="COM.icf::TRCTL.tom"/>
<include src="COM.icf::LockControl.tom"/>
<include src="ROM.icf::L2R05.tom"/>
<include src="ROM.icf::L2R07.tom"/>
<include src="ROM.icf::L2R09.tom"/>
<include src="ROM.icf::L3R04.tom"/>
<include src="ROM.icf::L3R05.tom"/>
<include src="RPDTL.icf::RPDTL.tom"/>
<include src="PRT.icf::FM101.tom"/>
<include src="HELPRIM.icf::HELPRIM.tom"/>
</sub>


!---------- 單據輸出組合 ----------
<sub type="SELECT">
<include src="PRT.icf::UPD01.sel"/>
<include src="PRT.icf::UPDH01.sel"/>
<include src="PRT.icf::FM01.sel"/>
<include src="PRT.icf::FMR01.sel"/>
<include src="PRT.icf::FMH01.sel"/>
<include src="PRT.icf::FMHR01.sel"/>
<include src="PRT.icf::FM101.sel" map="landscape=1;size=830:1170" />
#PdfPrtF=1,PDF,,,,,,,,,,,,,PdfSnoF,單筆匯款匯款單
^
</sub>
